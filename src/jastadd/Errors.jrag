import java.util.Set;
import java.util.TreeSet;

aspect Errors {
	public class ErrorMessage implements Comparable<ErrorMessage> {
		protected final String message;
		protected final int lineNumber;
		public ErrorMessage(String message, int lineNumber) {
			this.message = message;
			this.lineNumber = lineNumber;
		}
		public int compareTo(ErrorMessage other) {
			if (lineNumber == other.lineNumber) {
				return message.compareTo(other.message);
			}
			return Integer.compare(lineNumber, other.lineNumber);
		}
		public String toString() {
			return "Error at line " + lineNumber + ": " + message;
		}
	}
	protected ErrorMessage ASTNode.error(String message) {
		return new ErrorMessage(message, getLine(getStart()));
	}

	coll Set<ErrorMessage> Program.errors() [new TreeSet<ErrorMessage>()] with add root Program;

	inh Program ASTNode.program();
	eq Program.getChild().program() = this;
}

aspect ErrorContributions {
	IdUse contributes error("symbol '" + getID() + "' is not declared")
		when decl().isUnknown()
		to Program.errors() for program();

	IdUse contributes error("symbol '" + getID() + "' is a function but used as a variable")
		when decl().isFunction() && isVariable()
		to Program.errors() for program();

	IdUse contributes error("symbol '" + getID() + "' is a variable but used as a function")
		when decl().isVariable() && isFunction()
		to Program.errors() for program();

	IdDecl contributes error("symbol '" + getID() + "' is already declared!")
		when isMultiDeclared()
		to Program.errors() for program();

	IdUse contributes error("the definition of symbol '" + getID() + "' is circular")
		when isCircular()
		to Program.errors() for program();

	FuncCall contributes error("The number of arguments given to '" + getIdUse().getID() + "' does not match its parameters")
		when argNbrMismatch()
		to Program.errors() for program();

	Expr contributes error("Incompatible types expected " + expectedType() + "' but was " + getType())
		when !expectedType().compatibleType(getType())
		to Program.errors() for program();


}
